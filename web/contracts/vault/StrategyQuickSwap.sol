// SPDX-License-Identifier: MIT

pragma solidity 0.6.12;

import './libs/IStakingRewards.sol';

import './BaseStrategyLPSingle.sol';

contract StrategyQuickSwap is BaseStrategyLPSingle {
    using SafeMath for uint256;
    using SafeERC20 for IERC20;

    address public quickSwapAddress;

    constructor(
        address _vaultChefAddress,
        address _quickSwapAddress,
        address _wantAddress,
        address _earnedAddress,
        address[] memory _earnedToWmaticPath,
        address[] memory _earnedToUsdcPath,
        address[] memory _earnedToMushPath,
        address[] memory _earnedToToken0Path,
        address[] memory _earnedToToken1Path,
        address[] memory _token0ToEarnedPath,
        address[] memory _token1ToEarnedPath
    ) public {
        govAddress = msg.sender;
        vaultChefAddress = _vaultChefAddress;

        wantAddress = _wantAddress;
        token0Address = IUniPair(wantAddress).token0();
        token1Address = IUniPair(wantAddress).token1();

        uniRouterAddress = 0xa5E0829CaCEd8fFDD4De3c43696c57F7D7A678ff;
        quickSwapAddress = _quickSwapAddress;
        earnedAddress = _earnedAddress;

        earnedToWmaticPath = _earnedToWmaticPath;
        earnedToUsdcPath = _earnedToUsdcPath;
        earnedToMushPath = _earnedToMushPath;
        earnedToToken0Path = _earnedToToken0Path;
        earnedToToken1Path = _earnedToToken1Path;
        token0ToEarnedPath = _token0ToEarnedPath;
        token1ToEarnedPath = _token1ToEarnedPath;

        transferOwnership(vaultChefAddress);

        _resetAllowances();
    }

    function _vaultDeposit(uint256 _amount) internal override {
        IStakingRewards(quickSwapAddress).stake(_amount);
    }

    function _vaultWithdraw(uint256 _amount) internal override {
        IStakingRewards(quickSwapAddress).withdraw(_amount);
    }

    function _vaultHarvest() internal override {
        IStakingRewards(quickSwapAddress).getReward();
    }

    function vaultSharesTotal() public view override returns (uint256) {
        return IStakingRewards(quickSwapAddress).balanceOf(address(this));
    }

    function wantLockedTotal() public view override returns (uint256) {
        return
            IERC20(wantAddress).balanceOf(address(this)).add(
                IStakingRewards(quickSwapAddress).balanceOf(address(this))
            );
    }

    function _resetAllowances() internal override {
        IERC20(wantAddress).safeApprove(quickSwapAddress, uint256(0));
        IERC20(wantAddress).safeIncreaseAllowance(
            quickSwapAddress,
            uint256(-1)
        );

        IERC20(earnedAddress).safeApprove(uniRouterAddress, uint256(0));
        IERC20(earnedAddress).safeIncreaseAllowance(
            uniRouterAddress,
            uint256(-1)
        );

        IERC20(token0Address).safeApprove(uniRouterAddress, uint256(0));
        IERC20(token0Address).safeIncreaseAllowance(
            uniRouterAddress,
            uint256(-1)
        );

        IERC20(token1Address).safeApprove(uniRouterAddress, uint256(0));
        IERC20(token1Address).safeIncreaseAllowance(
            uniRouterAddress,
            uint256(-1)
        );

        IERC20(usdcAddress).safeApprove(rewardAddress, uint256(0));
        IERC20(usdcAddress).safeIncreaseAllowance(rewardAddress, uint256(-1));
    }

    function _emergencyVaultWithdraw() internal override {
        IStakingRewards(quickSwapAddress).withdraw(vaultSharesTotal());
    }
}
